/*
 * Copyright (c) Sony Cyriac 2016.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'// must be applied after your artifact generating plugin (eg. java / com.android.library)

// This is the library version used when deploying the artifact
version = "1.0.1"

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"
    resourcePrefix "discreteslider_"


    defaultConfig {
        minSdkVersion 4
        targetSdkVersion 22
        versionCode 2
        versionName "1.0.1"

    }
    buildTypes {
    }
}

def siteUrl = 'https://github.com/sonyckovoor/Discrete-Slider'     // Homepage URL of the library
def gitUrl = 'https://github.com/sonyckovoor/Discrete-Slider.git'   // Git repository URL
def issueTracker = 'https://github.com/sonyckovoor/Discrete-Slider/issues'
group = "in.sc9"                                        // Maven Group ID for the artifact


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'Discrete Slider'
                url siteUrl
                description = 'Discrete Slider is an attempt to implement material style [Discrete Slider]. This project was forked from  AnderWebâ€™s discreteSeekBar and made quiet a bit of modifications to make it comply with Discrete Slider component from the Google Material Design Guidelines.'

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'sonyckovoor'
                        name 'Sony Cyriac'
                        email 'sonyckovoor@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "Discrete Slider"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.1.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}